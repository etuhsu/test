//嵌入脚本
import win.ui;
/*DSG{{*/
var winform = ..win.form(text="HTMLayout 嵌入脚本";right=599;bottom=399;parent=...)
winform.add(
layoutWindow={cls="edit";text="layoutWindow";left=10;top=12;right=580;bottom=382;db=1;dl=1;dr=1;dt=1;multiline=1;notify=1;z=1}
)
/*}}*/

import web.layout;
var wbLayout = web.layout(winform.layoutWindow);

import web.script;
namespace web.layout.behavior{
	namespace javascript{
		var javascript = ..web.script("JavaScript") 
		onAttach = function( ltEle ){
			javascript.document = ltEle.root();
			javascript.AddCode( ltEle.innerText ); 
			return true	
		}
	}
	namespace aauto{ 
		onAttach = function( ltEle ){  
			assert(  loadcode(  ltEle.innerText ) )( ltEle.getLayout()  )
			return true	
		}
	}
}
  
 
html =/***
	<div id="test">......</div>
	
	<script type='text/javascript'> 
		var ele = document.getElementById("test"); 
		ele.innerText = "Javascript执行结果:测试一下"
	</script>	
	
	<script type='text/aauto'> 
		import win; 
		
		var window = ...;
		var $ = function(...){
			return window.querySelectorAll(...);
		}
				
		var code = $("script[type='text/aauto']")[1].innerText
		win.msgbox( code ) 
	</script> 
***/

//添加默认CSS
wbLayout.appendMasterCss( "
	script[type='text/javascript']{ behavior:javascript; } 
	script[type='text/aauto'] { behavior:aauto; }
")
wbLayout.write(html) 

winform.show() 
win.loopMessage();
